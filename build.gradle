buildscript {
    ext.kotlin_version = '1.4.32'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.ridi'
version = '1.5.8'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 16
        versionCode 1
        versionName version
        consumerProguardFiles 'proguard-rules.pro'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    buildFeatures {
        dataBinding = true
    }
}

configurations {
    ktlint
}

dependencies {
    compileOnly 'androidx.fragment:fragment-ktx:1.2.5'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.ridi:android-gpuimage:v2.1.1'
    // compress 1.13+ requires API 26+ (java.nio.file) in order to use ZipFile
    implementation 'org.apache.commons:commons-compress:1.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation 'junit:junit:4.13'

    ktlint 'com.pinterest:ktlint:0.34.2'
    ktlint 'com.github.ridi:ktlint-ruleset:master-SNAPSHOT'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

task ktlint(type: JavaExec, group: 'verification') {
    description = 'Check Kotlin code style.'
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args 'src/**/*.kt'
}

afterEvaluate { project ->
    check.dependsOn ktlint
}

task ktlintFormat(type: JavaExec, group: 'formatting') {
    description = 'Fix Kotlin code style deviations.'
    classpath = configurations.ktlint
    main = 'com.pinterest.ktlint.Main'
    args '-F', 'src/**/*.kt'
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
        }
    }
}
